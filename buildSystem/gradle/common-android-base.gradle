apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'com.google.devtools.ksp'
apply plugin: 'com.adarshr.test-logger'
apply plugin: 'de.mannodermaus.android-junit5'
apply plugin: 'kover'

android {
    compileSdkVersion AppConfig.COMPILE_SDK_VERSION
    buildToolsVersion AppConfig.BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion AppConfig.MIN_SDK_VERSION
        targetSdkVersion AppConfig.TARGET_SDK_VERSION
        versionCode AppVersion.CODE
        versionName AppVersion.NAME

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        consumerProguardFiles 'proguard-rules.pro'
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            buildConfigField 'String', 'APP_PACKAGE_NAME', '"' + AppConfig.APP_PACKAGE_NAME_DEBUG + '"'
        }
        release {
            debuggable false
            minifyEnabled false
            buildConfigField 'String', 'APP_PACKAGE_NAME', '"' + AppConfig.APP_PACKAGE_NAME + '"'
        }
    }

    buildFeatures {
        buildConfig true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        freeCompilerArgs = ['-Xstring-concat=inline']
        jvmTarget = JavaVersion.VERSION_17
    }

    testOptions {
        unitTests.all {
            it.useJUnitPlatform()
            if (name == "testDebugUnitTest") {
                kover {
                    enabled = true
                    reportFile.set(file("$buildDir/reports/kover/debug-report.bin"))
                    includes = ['com.alxnophis.jetpack.*']
                    excludes = []
                }
            }
        }
    }

    lint {
        abortOnError false
        checkDependencies true
        checkReleaseBuilds false
        ignoreTestSources true
        htmlOutput file("$buildDir/reports/lint/lint-results.html")
        xmlOutput file("$buildDir/reports/lint/lint-results.xml")
        disable += [
                'LogNotTimber',
                'StringFormatInTimber',
                'ThrowableNotAtBeginning',
                'BinaryOperationInTimber',
                'TimberArgCount',
                'TimberArgTypes',
                'TimberTagLength',
                'TimberExceptionLogging'
        ]
    }

    packagingOptions {
        resources {
            excludes += [
                    '**/attach_hotspot_windows.dll',
                    'META-INF/licenses/**',
                    'META-INF/AL2.0',
                    'META-INF/LGPL2.1',
            ]
        }
    }
}
