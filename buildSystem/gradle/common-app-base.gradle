apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion AppConfig.COMPILE_SDK_VERSION
    buildToolsVersion AppConfig.BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId AppConfig.APPLICATION_ID
        minSdkVersion AppConfig.MIN_SDK_VERSION
        targetSdkVersion AppConfig.TARGET_SDK_VERSION
        versionCode AppVersion.CODE
        versionName AppVersion.NAME

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled false
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            enableAndroidTestCoverage false
            minifyEnabled false
            ext.enableCrashlytics = false
        }
        release {
            debuggable false
            enableAndroidTestCoverage false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def project = "jetpack"
            def apkExtension = ".apk"
            def SEP = "_"
            def buildType = name
            def versionName = variant.versionName
            def versionCode = versionCode
            def date = new Date()
            def formattedDate = date.format('ddMMyy_HHmm')
            def newApkName = project + SEP + buildType +
                    SEP + versionCode + SEP + versionName +
                    SEP + formattedDate + apkExtension
            outputFileName = new File(newApkName)
        }
        if (variant.buildType.name == "debug") {
            variant.mergedFlavor.resourceConfigurations.add("xxhdpi")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    lint {
        abortOnError false
        checkDependencies true
        checkReleaseBuilds false
        ignoreTestSources true
        htmlOutput file("$buildDir/reports/lint/lint-results.html")
        xmlOutput file("$buildDir/reports/lint/lint-results.xml")
        disable += [
                'LogNotTimber',
                'StringFormatInTimber',
                'ThrowableNotAtBeginning',
                'BinaryOperationInTimber',
                'TimberArgCount',
                'TimberArgTypes',
                'TimberTagLength',
                'TimberExceptionLogging'
        ]
    }

    packagingOptions {
        resources {
            excludes += [
                    '**/attach_hotspot_windows.dll',
                    'META-INF/licenses/**',
                    'META-INF/AL2.0',
                    'META-INF/LGPL2.1',
            ]
        }
    }
}

dependencies {
    implementation project(Modules.SHARED_CORE)
    implementation project(Modules.SHARED_KOTLIN)
    implementation project(Modules.SHARED_ROUTER)

    implementation libs.androidx.appcompat
    implementation libs.koin.android

    testImplementation libs.junit.jupiter.api
    testImplementation libs.junit.jupiter.params
    testImplementation libs.koin.test

    testRuntimeOnly libs.junit.jupiter.engine

    androidTestImplementation libs.androidx.test.junit
    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.androidx.test.rules
    androidTestImplementation libs.androidx.arch.core.testing
}
