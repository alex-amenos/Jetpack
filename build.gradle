buildscript {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath 'com.adarshr:gradle-test-logger-plugin:3.2.0'
        classpath 'com.android.tools.build:gradle:7.4.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.42.0'
        classpath "com.apollographql.apollo3:apollo-gradle-plugin:${Version.APOLLO}"
        classpath 'de.mannodermaus.gradle.plugins:android-junit5:1.8.2.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${Version.KOTLIN}"
        classpath 'org.jetbrains.kotlinx:kover:0.6.1'
        classpath 'org.jlleitschuh.gradle:ktlint-gradle:11.0.0'
    }
}

plugins {
    id 'org.jetbrains.kotlinx.kover' version '0.6.1'
    id 'com.android.application' version '7.4.0' apply false
    id 'org.jetbrains.kotlin.android' version '1.7.21' apply false
}

allprojects {
    tasks.withType(Test) {
        testLogging {
            exceptionFormat 'full'
            events 'started', 'skipped', 'passed', 'failed'
            showStandardStreams true
            outputs.upToDateWhen { false }
        }
    }
}

subprojects {
    apply from: "$rootDir/buildSystem/gradle/ktlint.gradle"
    apply from: "$rootDir/buildSystem/gradle/dependency-updates.gradle"
}

task clean(type: Delete) {
    delete buildDir
}

task installGitHook(type: Copy) {
    from new File(rootProject.rootDir, 'buildSystem/githooks/prepare-commit-msg')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0755
}

tasks.getByPath(':app:preBuild').dependsOn installGitHook

// Multi-module reports for instrumented tests
// https://developer.android.com/studio/test/command-line#multi-module-reports-instrumented-tests
apply plugin: 'android-reporting'

// Helpful example:
// https://github.com/Kotlin/kotlinx-kover/blob/release/examples/android_groovy/build.gradle
koverMerged {
    enable()

    filters {
        classes {
            includes.addAll("com.alxnophis.jetpack.*")
            excludes.addAll(
                    'com.alxnophis.jetpack.core.base.*',
                    'com.alxnophis.jetpack.*.di.*',
                    'com.alxnophis.jetpack.di.*',
                    'com.alxnophis.jetpack.core.base.*',
                    'com.alxnophis.jetpack.router.*',
                    'com.alxnophis.jetpack.*.ui.navigation.*',
                    "*Activity",
                    "BuildConfig",
                    '*Mother',
                    '*NavGraph\$*',
            )
        }

        projects {
            excludes.addAll(
                    ":app",
                    ":shared:api",
                    ":shared:testing"
            )
        }
    }
}
